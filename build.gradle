apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'

sourceCompatibility = '1.8'
mainClassName  = "org.ignis.backend.Main"
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-proc:none"
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocsJar(type: Jar, dependsOn: javadoc, description: 'Creates a jar from the javadocs files.') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocsJar
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        groupId = 'org.ignis.backend'
        artifactId = 'org.ignis.backend'
        version = '1.0'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.thrift', name: 'libthrift', version: '0.11.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.5'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.11.0'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
}

task copyToLib(type: Copy, description: 'Copy project dependencies.') {
    from configurations.compile
    into "$buildDir/libs"
}
build.dependsOn(copyToLib)

task jarAndLibs {
    dependsOn 'jar'
    dependsOn 'copyToLib'
}
