apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'application'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
mainClassName = 'org.ignis.backend.Main'

javadoc {
    options.addBooleanOption('html5', true)
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocsJar(type: Jar, dependsOn: javadoc, description: 'Creates a jar from the javadocs files.') {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocsJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.ignis'
            artifactId = 'backend'
            version = '1.0'

            from components.java
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.thrift', name: 'libthrift', version: '0.13.0'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.14.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
    implementation group: 'com.mesosphere', name: 'marathon-client', version: '0.6.3'
    implementation group: 'com.hubspot', name: 'SingularityClient', version: '1.4.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.3'
    implementation group: 'com.github.mwiede', name: 'jsch', version: '0.1.62'
    implementation group: 'info.picocli', name: 'picocli', version: '4.5.2'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.16'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
    annotationProcessor group: 'info.picocli', name: 'picocli', version: '4.5.2'

    //testCompile group: 'junit', name: 'junit', version: '4.12'
    //testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
    //testCompile group: 'org.mockito', name: 'mockito-core', version: '2.22.0'
    //testRuntimeOnly group : 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'
}

/*
test {
    useJUnitPlatform{
        includeEngines 'junit-jupiter'
    }
}*/

task copyToLib(type: Copy, description: 'Copy project dependencies.') {
    from project.configurations.compileClasspath
    into "$buildDir/libs"
}
build.dependsOn(copyToLib)

task jarAndLibs {
    dependsOn 'jar'
    dependsOn 'copyToLib'
}
